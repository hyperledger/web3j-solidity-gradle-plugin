plugins {
    id 'idea'
    id 'maven-publish'
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.2.1'
    id 'com.diffplug.spotless' version '6.23.3'
    id 'de.undercouch.download' version '4.1.1'
}

apply {
    [
        'jacoco',
        'java',
        'javadoc',
        'repositories',
        'spotless'
    ].each { buildScript ->
        download {
            src "https://raw.githubusercontent.com/hyperledger/web3j-build-tools/main/gradle/$buildScript/build.gradle"
            dest "$rootDir/gradle/$buildScript/build.gradle"
            overwrite false
            quiet true
            onlyIfModified true
        }
        from("$rootDir/gradle/$buildScript/build.gradle")
    }
}

description = 'Solidity Gradle Plugin'

gradlePlugin {
    plugins {
        solidity {
            id = 'org.web3j.solidity'
            implementationClass = 'org.web3j.solidity.gradle.plugin.SolidityPlugin'
        }
    }
}

gradlePlugin {
    website = 'https://web3j.io/'
    vcsUrl = 'https://github.com/hyperledger/web3j-solidity-gradle-plugin'
    description = 'Gradle plugin providing tasks to compile Solidity contracts.'
    plugins {
        solidity {
            // id is captured from java-gradle-plugin configuration
            displayName = project.description
            tags = ['solidity', 'ethereum']
        }
    }
}
repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

ext {
    soktVersion = '0.3.0'
    junitVersion = '4.13'
    assertjVersion = '3.17.1'
    systemRulesVersion = '1.19.0'
    nodePluginVersion = '7.0.1'
}

dependencies {
    implementation "org.web3j:web3j-sokt:$soktVersion"
    implementation "com.github.node-gradle:gradle-node-plugin:$nodePluginVersion"
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation "com.github.stefanbirkner:system-rules:$systemRulesVersion"
}

spotless {
    groovy {
        excludeJava() // excludes all Java sources within the Groovy source dirs from formatting
        licenseHeaderFile "$rootDir/gradle/spotless/java.license"
    }
}
