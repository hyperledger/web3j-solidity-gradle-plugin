plugins {
    id "com.gradle.plugin-publish" version "0.10.0"
    id "java-gradle-plugin"
    id 'com.jfrog.bintray' version '1.8.4'
    id 'java'
    id 'groovy'

    // Required for JFrog Artifactory repository
    id 'jacoco'
    id 'maven-publish'

    // Required for Maven Nexus repository
    id 'maven'
    id 'signing'
    id 'idea'
}

group = 'org.web3j'
version = '0.1.0'
description = 'Solidity Gradle Plugin'
sourceCompatibility = 1.8

gradlePlugin {
    plugins {
        solidity {
            id = 'org.web3j.solidity'
            implementationClass = 'org.web3j.solidity.gradle.plugin.SolidityPlugin'
        }
    }
}

pluginBundle {
    website = 'https://web3j.io/'
    vcsUrl = 'https://github.com/web3j/solidity-gradle-plugin'
    description = 'Gradle plugin providing tasks to compile Solidity contracts.'
    tags = ['solidity', 'ethereum']
    plugins {
        solidity {
            // id is captured from java-gradle-plugin configuration
            displayName = project.description
        }
    }
}

jacoco {
    toolVersion = '0.8.1' // See http://www.eclemma.org/jacoco/.
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

ext {
    junitVersion = '4.12'
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile "junit:junit:$junitVersion"
}

tasks.withType(Test) {
    reports.html.destination file("${reporting.baseDir}/${project.name}")
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

def pomConfig = {
    scm {
        url 'https://github.com/web3j/solidity-gradle-plugin'
        connection 'scm:https://github.com/web3j/solidity-gradle-plugin.git'
        developerConnection 'scm:git://github.com/web3j/solidity-gradle-plugin.git'
    }
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'conor10'
            name 'Conor Svensson'
            email 'conor10@gmail.com'
        }
        developer {
            id 'xaviarias'
            name 'Xavier Arias'
            email 'xavi.arias.segui@gmail.com'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.name
            version project.version
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                def root = asNode()
                root.appendNode('name', project.description)
                root.appendNode('description', 'Gradle plugin providing tasks to compile Solidity contracts.')
                root.appendNode('url', 'https://github.com/web3j/solidity-gradle-plugin')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven'
        name = project.name
        desc = project.description
        userOrg = 'web3j'
        licenses = ['Apache-2.0']
        issueTrackerUrl = 'https://github.com/web3j/solidity-gradle-plugin/issues'
        vcsUrl = 'https://github.com/web3j/solidity-gradle-plugin.git'
        websiteUrl = 'http://web3j.io'
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = project.description
            vcsTag = project.version
            released = new Date()
            gpg {
                sign = true
                passphrase = project.property('signing.password')
            }
            mavenCentralSync {
                sync = true
                user = project.property('nexusUsername')
                password = project.property('nexusPassword')
                close = '1'
            }
        }
    }
}

task release {
    dependsOn 'clean'
    dependsOn 'build'
    dependsOn 'bintrayUpload'

    tasks.findByName('build').mustRunAfter 'clean'
    tasks.findByName('bintrayUpload').mustRunAfter 'build'
}

